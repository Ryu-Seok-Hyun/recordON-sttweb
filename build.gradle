plugins {
	id 'java'
	id 'war'
	id 'org.springframework.boot' version '3.4.4'
	id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.sttweb'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

// 내장용 fat JAR/Bootable WAR 비활성화
bootJar {
	enabled = false
}
bootWar {
	enabled = false
}

// Plain WAR 하나만 생성 (classifier 제거 + 이름 고정)
war {
	enabled = true
	// default plain-war 에 붙는 "-plain" classifier 를 지웁니다
	archiveClassifier = ''
	// 톰캣에 올릴 파일명을 명시적으로 지정
	archiveFileName = 'ROOT.war'
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	// 서블릿 API (컴파일 타임에만 필요)
	compileOnly 'jakarta.servlet:jakarta.servlet-api:6.0.0'

	// 내장 톰캣 제외
	implementation('org.springframework.boot:spring-boot-starter-web') {
		exclude group: 'org.springframework.boot', module: 'spring-boot-starter-tomcat'
	}
	// 외부 톰캣 제공
	providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'

	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity6'

	implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
	runtimeOnly  'io.jsonwebtoken:jjwt-impl:0.11.5'
	runtimeOnly  'io.jsonwebtoken:jjwt-jackson:0.11.5'

	implementation 'org.springframework.boot:spring-boot-starter-validation'
	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'org.mariadb.jdbc:mariadb-java-client'
	annotationProcessor 'org.projectlombok:lombok'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

springBoot {
	mainClass = 'com.sttweb.sttweb.SttwebApplication'
}

tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
	options.compilerArgs += ['-parameters']
}
